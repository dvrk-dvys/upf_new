(define (domain sokoban)
  (:requirements :typing :negative-preconditions)
  (:types thing location action - object
          player box - thing)
  (:predicates (clear ?l - location)
	       (at ?t - thing ?l - location)
	       (at_goal ?s - box)
	       (IS-GOAL ?l - location)
	       (IS-NONGOAL ?l - location)
           (MOVE-DIR ?from ?to - location ?act - action)
	       (teleported))

  (:action move
   :parameters (?p - player ?from ?to - location ?act - action)
   :precondition (and (at ?p ?from)
                      (clear ?to)
                      (MOVE-DIR ?from ?to ?act)
                      )
   :effect       (and (not (at ?p ?from))
                      (not (clear ?to))
                      (at ?p ?to)
                      (clear ?from)
                      )
   )

   (:action teleport
   :parameters (?p - player ?from ?to - location ?act - action)
   :precondition (and (at ?p ?from)
                      (clear ?to)
                      (MOVE-DIR ?from ?to ?act)
                      (not (teleported))
                      )
   :effect       (and (not (at ?p ?from))
                      (not (clear ?to))
                      (at ?p ?to)
                      (clear ?from)
                      (teleported)
                      )
   )

  (:action push-to-nongoal
   :parameters (?p - player ?s - box
                ?ppos ?from ?to - location
                ?act - action)
   :precondition (and (at ?p ?ppos)
                      (at ?s ?from)
                      (clear ?to)
                      (MOVE-DIR ?ppos ?from ?act)
                      (MOVE-DIR ?from ?to ?act)
                      (IS-NONGOAL ?to)
                      )
   :effect       (and (not (at ?p ?ppos))
                      (not (at ?s ?from))
                      (not (clear ?to))
                      (at ?p ?from)
                      (at ?s ?to)
                      (clear ?ppos)
                      (not (at_goal ?s))
                      )
   )

  (:action push-to-goal
   :parameters (?p - player ?s - box
                ?ppos ?from ?to - location
                ?act - action)
   :precondition (and (at ?p ?ppos)
                      (at ?s ?from)
                      (clear ?to)
                      (MOVE-DIR ?ppos ?from ?act)
                      (MOVE-DIR ?from ?to ?act)
                      (IS-GOAL ?to)
                      )
   :effect       (and (not (at ?p ?ppos))
                      (not (at ?s ?from))
                      (not (clear ?to))
                      (at ?p ?from)
                      (at ?s ?to)
                      (clear ?ppos)
                      (at_goal ?s)
                      )
   )
)
